    1REM *FX 200,3
    2*FX 4,1
   10MODE 1
   20HIMEM=&2D60
   30*LOAD OBJASM2 2D60
   40*LOAD OBJASM1 900
   45MC%=&2D60
   50VDU 23,1,0;0;0;0,0
   60COLOUR1
   61PROCVECTOR
   62VDU 19,1,2,0;0:VDU 19,2,6,0;0:VDU 19,3,5,0;0
   70DIM Grid%(7,9):Grid_xpos%=2:Grid_ypos%=1:Xdim%=3:Ydim%=5
   80PROCIntro
   84CLS
   85PROCPrint_title
   90CHAIN"MAIN"
  100END
  110DEF PROCSetup_grid
  120P%=0:R%=0:C%=0:A$="":B%=0
  130REPEAT
  140V%=VAL( RIGHT$( Level$, LEN(Level$)-P% ) )
  150IF V%>0 FOR I%=1 TO V%:Grid%(R%,C%)=0:C%=C%+1:NEXT:A$=""
  160IF V%>9 P%=P%+2 ELSE IF V%>0:IF V%<10 P%=P%+1
  170IF V%=0 A$=MID$(Level$,P%+1,1) ELSE GOTO 220
  180IF A$="/" P%=P%+1
  190IF A$="~" Grid%(R%,C%)=9:C%=C%+1:P%=P%+1
  200IF A$<>"":IF A$<>"/":IF A$<>"~" Grid%(R%,C%)=ASC(A$)-96:C%=C%+1:P%=P%+1:B%=B%+1
  210IF C%>=Xdim% C%=0:R%=R%+1
  220UNTIL R%=Ydim%
  230ENDPROC
  240:
  250DEFPROCDraw_grid(C%,D%,E%,F%)
  260LOCAL I%
  270FOR I%=0 TO Ydim%-1
  280FOR J%=0 TO Xdim%-1
  290?&70=Grid%(I%,J%)
  300?&71=Grid_xpos%+J%*4 : ?&72=Grid_ypos%+I%*2
  310IF ?&70=0 ?&73=D%:?&74=C%:CALL MC%:GOTO 340
  320IF ?&70>0:IF ?&70<9 ?&73=F%:?&74=E%:CALL MC%:GOTO 340
  330IF ?&70=9 ?&73=0:CALL MC%
  340NEXT,
  350ENDPROC
  360:
  370DEFPROCDraw_item(G%,H%,S%,FC%,BC%)
  380?&70=S%:?&71=G%:?&72=H%:?&73=FC%:?&74=BC%
  390CALL MC%
  400ENDPROC
  410:
  420DEFPROCPrint_title
  430LOCAL I%
  440FI%=OPENIN("TITLE")
  450FOR I%=0 TO 4
  460INPUT#FI%,Level$
  470PROCSetup_grid
  480PROCDraw_grid(0,0,(I% MOD3)+1,((I%+2)MOD3)+1)
  490Grid_xpos%=Grid_xpos%+16
  500NEXT
  510CLOSE#FI%
  520ENDPROC
  530DEFPROCIntro
  540PRINTTAB(0,14);
  550COLOUR1
  560PRINT"Vexed was an award-winning puzzle game  for Palm OS devices. It's got all the   attributes of an addictive, fun game.   It's easy to learn, but vexing to"'"master. Give it a try, and you'll see   what we mean."
  570COLOUR2
  580PRINT'"The goal of the game is to move similar blocks together, which causes them to   disappear. Once all the blocks are gone,you've solved the level and you're"'"presented with a new level to solve."
  590*FX 21,0
  600A=GET
  610PROCClear
  620PRINTTAB(0,14);
  630COLOUR3
  640PRINT"You move blocks by putting the cursor   over them, hit RETURN to select them,   and move them left or right. If an emptyspace is encoutered, gravity takes over and the blocks fall towards the bottom"
  650COLOUR1
  660PRINT'"The controls for moving the cursor are"'"Z = LEFT X = RIGHT * = UP ? = DOWN"
  670COLOUR2
  680PRINT'"The challenge of the game is to solve itwithin the same or less moves than"'"stored in it's solution. If you get"'"stuck you can see the solution play out on the screen (S). (R) will reset the   current level.";
  690PRINT;"(Q) to quit and save your current progres";
  700*FX 21,0
  710A=GET
  720PROCClear
  721?&2FFF=0
  730PRINTTAB(0,14)"Choose your level pack"
  740PRINTTAB(0,16)"A) Classic Levels I"
  750PRINTTAB(0,17)"B) Classic Levels II"
  751PRINTTAB(0,18)"C) Confusion Pack"
  752PRINTTAB(0,19)"D) Panic Pack"
  760REPEAT:A$=GET$:UNTILINSTR("ABCD",A$)<>0
  770$&2FF8=A$
  780F%=OPENIN(A$+"1")
  790IF F%=0 CLOSE#0:GOTO 850
  795PRINTTAB(0,21)"Continue from a previous save game (Y/N)?";
  800REPEAT:B$=GET$:UNTILINSTR("YyNn",B$)
  810IF INSTR("Nn",B$) <> 0 CLOSE#F%:GOTO 850
  820INPUT#F%,L%
  830CLOSE#F%
  840?&2FFF=L%
  850PROCClear
  860ENDPROC
  870DEFPROCClear
  880FOR A%=14 TO 30
  890PRINTTAB(0,A%)STRING$(40," ");
  900NEXT:ENDPROC
 1000 DATA 0,-256,192,0,0,256,-64,0,0,-320,-64,0,0,320,-64,0
 1010 DATA  0,-320,192,0,0,64,-128,0,0,64,64,0,0,64,-64,0,0,64,128,0,0,64,-192,0
 1020DATA 0,-128,192,0,0,128,-64,0,0,-320,64,0,0,128,-192,0,0,-128,64,0,0,320,-64,0
 1030DATA 0,-320,128,0,0,64,-64,0,0,192,128,0,0,-192,-64,0,0,256,-128,0
 1035DEFPROCVECTOR
 1040C%=160:D%=96
 1050VDU 19,1,4,0,0,0
 1060PROCLETTER(128,828,1000,8)
 1070PROCLETTER(384,828,1010,12)
 1080PROCLETTER(640,828,1020,12)
 1090PROCLETTER(896,828,1010,12)
 1100PROCLETTER(1152,828,1030,10)
 1110ENDPROC
 1120DEFPROCLETTER(X%,Y%,R%,P%)
 1130MOVE X%,Y%
 1140GCOL 3,1
 1150FOR I%=1 TO 10
 1160F=I%/10
 1170PROCDRAW(X%,Y%,R%,P%,F)
 1180PROCDRAW(X%,Y%,R%,P%,F)
 1190NEXT
 1200GCOL 0,3
 1210PROCDRAW(X%,Y%,R%,P%,1)
 1220ENDPROC
 1230:
 1240DEFPROCDRAW(x%,y%,R%,P%,F)
 1250MOVE x%-(D%*F),y%+(C%*F)
 1260RESTORE R%
 1270FOR B%=1 TO P%
 1280READ X,Y
 1290PLOT 1,X*F,Y*F
 1300NEXT
 1310ENDPROC